#compdef envoy envoyd envoy-exec

zstyle -a ":completion:${curcontext}:" environ environ

case "$service" in
envoy)
  _arguments -s \
    {-h,--help}'[display this help]'\
    {-v,--version}'[display version]'\
    {-d,--defer}'[defer adding keys until the next envoy invocation]'\
    {-a,--add}'[add private key identities]':files:_files \
    {-x,--expunge}'[remove private key identities]'\
    {-k,--kill}'[kill the running agent]'\
    {-r,--reload}'[reload the agent (gpg-agent only)]'\
    {-l,--list}'[list fingerprints of all loaded identities]'\
    {-u,--unlock=-}'[unlock the agent''s keyring (gpg-agent only)]'\
    {-p,--print}'[print out environmental arguments]' \
    {-s,--sh}'[print sh style commands]' \
    {-c,--csh}'[print csh style commands]' \
    {-f,--fish}'[print fish style commands]' \
    {-t,--agent=-}'[set the preferred agent to start]:agents:(ssh-agent gpg-agent)'
  ;;
envoyd)
  _arguments -s \
    {-h,--help}'[display this help]'\
    {-v,--version}'[display version]'\
    {-t,--agent=-}'[set the preferred agent to start]:agents:(ssh-agent gpg-agent)'
  ;;
envoy-exec)
  _arguments -s '*::arguments: _normal'
  ;;
esac
